#!/usr/bin/env coffee
##
# hubot [options]
#
# Launch an interactive hubot
#

Hubot      = require '..'
Fs         = require 'fs'
Path       = require 'path'
HTTP       = require 'http'
Creator    = require '../src/creator'
OptParse   = require 'optparse'
PortNumber = parseInt(process.env.PORT) || 8080

Adapters = ["irc", "campfire", "hipchat", "twilio", "xmpp", "groupme", "talker", "twitter", "flowdock", "gtalk"]

Switches = [
  [ "-h", "--help",              "Display the help information"],
  [ "-a", "--adapter ADAPTER",   "The Adapter to use"],
  [ "-c", "--create PATH",       "Create a deployable hubot"],
  [ "-s", "--enable-slash",      "Enable replacing the robot's name with '/'"],
  [ "-n", "--name NAME",         "The name of the robot in chat" ],
  [ "-v", "--version",           "Displays the version of hubot installed"]
]

Options =
  path: "."
  name: "Hubot"
  create: false
  adapter: "stdio"

Parser = new OptParse.OptionParser(Switches)
Parser.banner = "Usage hubot [options]"

Parser.on "adapter", (opt, value) ->
  Options.adapter = value

Parser.on "name", (opt, value) ->
  Options.name = value

Parser.on "create", (opt, value) ->
  Options.path = value
  Options.create = true

Parser.on "enable-slash", (opt) ->
  Options.enableSlash = true

Parser.on "help", (opt, value) ->
  console.log Parser.toString()
  process.exit 0

Parser.on "version", (opt, value) ->
  Options.version = true

Parser.parse process.ARGV

process.on 'SIGTERM', -> process.exit(0)

if Options.create
  creator = new Creator.Creator(Options.path)
  creator.run()

else if Options.version
  package_path = __dirname + "/../package.json"

  Fs.readFile package_path, (err,data) ->
    if err
      console.error "Could not open package file : %s", err
      process.exit 1

    content = JSON.parse(data.toString('ascii'))
    console.log content['version']

    process.exit 0

else
  adapter = if Options.adapter in Adapters
    Options.adapter
  else
    'shell'

  scriptsPath = Path.resolve "./scripts"
  console.log "Loading deploy-local scripts at #{scriptsPath}"
  robot = Hubot.loadBot adapter, scriptsPath, Options.name
  robot.enableSlash = Options.enableSlash

  scriptsPath = Path.resolve "src", "hubot", "scripts"
  console.log "Loading hubot core scripts for relative scripts at #{scriptsPath}"
  robot.load scriptsPath

  scriptsFile = Path.resolve "hubot-scripts.json"
  Path.exists scriptsFile, (exists) =>
    if exists
      console.log "Loading hubot-scripts from #{scriptsFile}"
      Fs.readFile scriptsFile, (err, data) =>
        JSON.parse(data).forEach (plugin) =>
          robot.loadFile Path.resolve("node_modules", "hubot-scripts", "src", "scripts"), plugin
    else
      scriptsPath = Path.resolve "src", "scripts"
      console.log "Loading hubot-scripts plugins at #{scriptsPath}"
      robot.load scriptsPath

  robot.run()

# vim:ft=coffee ts=2 sw=2 et :
