#!/usr/bin/env coffee

##
# hubot [options]
#
# Launch an interactive hubot
#
# vim:ft=coffee ts=2 sw=2 et :
# -*- mode:coffee -*-

Hubot      = require '..'
Fs         = require 'fs'
Path       = require 'path'
HTTP       = require 'http'
Creator    = require '../src/creator'
OptParse   = require 'optparse'

Switches = [
  [ "-a", "--adapter ADAPTER", "The Adapter to use" ],
  [ "-c", "--create PATH",     "Create a deployable hubot" ],
  [ "-d", "--disable-httpd",   "Disable the HTTP server" ],
  [ "-h", "--help",            "Display the help information" ],
  [ "-l", "--alias ALIAS",     "Enable replacing the robot's name with alias" ],
  [ "-n", "--name NAME",       "The name of the robot in chat" ],
  [ "-r", "--require PATH",    "Alternative scripts path" ],
  [ "-s", "--enable-slash",    "Enable replacing the robot's name with '/' (deprecated)" ],
  [ "-v", "--version",         "Displays the version of hubot installed" ]
]

Options =
  adapter: "shell"
  alias: false
  create: false
  enableHttpd: true
  scripts: []
  name: "Hubot"
  path: "."

Parser = new OptParse.OptionParser(Switches)
Parser.banner = "Usage hubot [options]"

Parser.on "adapter", (opt, value) ->
  Options.adapter = value

Parser.on "create", (opt, value) ->
  Options.path = value
  Options.create = true

Parser.on "disable-httpd", (opt) ->
  Options.enableHttpd = false

Parser.on "help", (opt, value) ->
  console.log Parser.toString()
  process.exit 0

Parser.on "alias", (opt, value) ->
  Options.alias = value

Parser.on "name", (opt, value) ->
  Options.name = value

Parser.on "require", (opt, value) ->
  Options.scripts.push(value)

Parser.on "enable-slash", (opt) ->
  console.log "WARNING: -s and --enable-slash are deprecated please use -l or --alias '/'"
  Options.alias = '/'

Parser.on "version", (opt, value) ->
  Options.version = true

Parser.parse process.argv

unless process.platform is "win32"
  process.on 'SIGTERM', ->
    process.exit 0

if Options.create
  creator = new Creator(Options.path)
  creator.run()

else
  adapterPath = Path.join __dirname, "..", "src", "adapters"

  robot = Hubot.loadBot adapterPath, Options.adapter, Options.enableHttpd, Options.name

  if Options.version
    console.log robot.version
    process.exit 0

  robot.enableSlash = Options.enableSlash
  robot.alias = Options.alias

  loadScripts = ->
    scriptsPath = Path.join __dirname, "..", "scripts"
    robot.load scriptsPath

    scriptsPath = Path.join __dirname, "..", "src", "scripts"
    robot.load scriptsPath

    scriptsFile = Path.join __dirname, "..", "hubot-scripts.json"
    Fs.exists scriptsFile, (exists) =>
      if exists
        Fs.readFile scriptsFile, (err, data) ->
          scripts = JSON.parse data
          scriptsPath = Path.join __dirname, "node_modules", "hubot-scripts", "src", "scripts"
          robot.loadHubotScripts scriptsPath, scripts

    for path in Options.scripts
      if path[0] == '/'
        scriptsPath = path
      else
        scriptsPath = Path.resolve ".", path
      robot.load scriptsPath

  robot.adapter.on 'connected', loadScripts

  robot.run()
